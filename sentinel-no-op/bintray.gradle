apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

task javadocsJar(type: Jar, dependsOn: rootProject.dokka) {
    archiveClassifier.set('javadoc')
    from rootProject.dokka.outputDirectory
}

afterEvaluate {
    publishing {
        publications {
            infinum(MavenPublication) {
                groupId = sentinel.group
                version = sentinel.version

                artifactId = 'sentinel-no-op'

                artifact bundleReleaseAar
                artifact sourcesJar
                artifact javadocsJar

                pom {
                    name = 'Sentinel no op'
                    description = 'No op version of single point of entry for all provided tools, app and device information.'
                    url = 'https://github.com/infinum/android-sentinel'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'knobtviker'
                            name = 'Bojan KomljenoviÄ‡'
                            email = 'bojan.komljenovic@infinum.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/infinum/android-sentinel.git'
                        developerConnection = 'https://github.com/infinum/android-sentinel.git'
                        url = 'https://github.com/infinum/android-sentinel'
                    }
                }
                pom.withXml {
                    def root = asNode()
                    def dependenciesNode = root.appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    def bintrayProperties = rootProject.file('bintray.properties')
    if (bintrayProperties.exists()) {
        properties.load(new FileInputStream(bintrayProperties))
    } else {
        properties.setProperty("bintray.user", "")
        properties.setProperty("bintray.apikey", "")
    }

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    publications = ["infinum"]

    pkg {
        repo = 'android'
        name = 'sentinel-no-op'
        userOrg = 'infinum'
        desc = 'Sentinel no op package'
        websiteUrl = 'https://github.com/infinum/android-sentinel'
        issueTrackerUrl = 'https://github.com/infinum/android-sentinel/issues'
        vcsUrl = 'https://github.com/infinum/android-sentinel.git'
        licenses = ['Apache-2.0']
        version {
            name = "${sentinel.version}"
            released = new Date()
            vcsTag = "${sentinel.version}"
        }
        publish = true
        override = true
        publicDownloadNumbers = true
    }
}
