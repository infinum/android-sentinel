buildscript {

    ext.buildConfig = [
            "minSdk"    : 21,
            "compileSdk": 30,
            "targetSdk" : 30,

            "buildTools": "30.0.2"
    ]

    apply from: "dependencies.gradle"

    repositories {
        google()
        jcenter()
        maven { url "https://dl.bintray.com/infinum/android" }
    }
    dependencies {
        classpath packages.gradle
        classpath packages.maven.plugin
        classpath packages.bintray
        classpath packages.kotlin.plugin
        classpath packages.detekt.plugin
        classpath packages.dokka.plugin
    }
}

allprojects {

    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "https://dl.bintray.com/infinum/android" }
    }

    def buildProperties = new Properties()
    file(rootDir.absolutePath+"/build.properties").withInputStream { buildProperties.load(it) }
    if (buildProperties.getProperty("build.debug").toBoolean()) {
        configurations.all {
            resolutionStrategy {
                dependencySubstitution {
                    substitute module(packages.sentinel.sentinel) with project(':sentinel')
                    substitute module(packages.sentinel.sentinel_no_op) with project(':sentinel-no-op')
                    substitute module(packages.sentinel.tool_chucker) with project(':tool-chucker')
                    substitute module(packages.sentinel.tool_collar) with project(':tool-collar')
                    substitute module(packages.sentinel.tool_dbinspector) with project(':tool-dbinspector')
                    substitute module(packages.sentinel.tool_googleplay) with project(':tool-googleplay')
                    substitute module(packages.sentinel.tool_thimble) with project(':tool-thimble')
                }
            }
        }
    }
}

subprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://dl.bintray.com/infinum/android" }
    }

    apply from: "../detekt.gradle"
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["--release", "8"]
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detektAll(dependsOn: [
        ':sentinel:detekt',
        ':sentinel-no-op:detekt',
        ':tool-chucker:detekt',
        ':tool-collar:detekt',
        ':tool-dbinspector:detekt',
        ':tool-googleplay:detekt',
        ':tool-thimble:detekt'
]) {
    group = "Verification"
    description = "Run detekt on all modules"
}

apply from: "deploy.gradle"

apply from: "dokka.gradle"