buildscript {

    ext.buildConfig = [
            "minSdk"    : 21,
            "compileSdk": 29,
            "targetSdk" : 29,

            "buildTools": "30.0.0"
    ]

    apply from: "dependencies.gradle"

    repositories {
        google()
        jcenter()
        maven { url "https://dl.bintray.com/infinum/android" }
    }
    dependencies {
        classpath packages.gradle
        classpath packages.maven.plugin
        classpath packages.bintray
        classpath packages.kotlin.plugin
        classpath packages.detekt.plugin
        classpath packages.dokka.plugin
    }
}

allprojects {

    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "https://dl.bintray.com/infinum/android" }
    }

    configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                substitute module(packages.sentinel.sentinel) with project(':sentinel')
                substitute module(packages.sentinel.sentinel_no_op) with project(':sentinel-no-op')
                substitute module(packages.sentinel.tool_chucker) with project(':tool-chucker')
                substitute module(packages.sentinel.tool_collar) with project(':tool-collar')
                substitute module(packages.sentinel.tool_dbinspector) with project(':tool-dbinspector')
                substitute module(packages.sentinel.tool_googleplay) with project(':tool-googleplay')
            }
        }
    }
}

subprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://dl.bintray.com/infinum/android" }
    }

    apply from: "../detekt.gradle"
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["--release", "8"]
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detektAll(dependsOn: [
        ':sentinel:detekt',
        ':sentinel-no-op:detekt',
        ':tool-chucker:detekt',
        ':tool-collar:detekt',
        ':tool-dbinspector:detekt',
        ':tool-googleplay:detekt'
]) {
    group = "Verification"
    description = "Run detekt on all modules"
}

apply from: "deploy.gradle"

apply from: "dokka.gradle"