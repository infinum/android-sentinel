buildscript {

    apply from: "config.gradle"
    apply from: "maven.gradle"

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath libs.tools.gradle
        classpath libs.kotlin.gradle
        classpath libs.kotlin.serialization
        classpath libs.cpd
        classpath libs.detekt.gradle
        classpath libs.ktlint
        classpath libs.dokka
    }
}

plugins {
    alias(libs.plugins.kotlin.binary.compatibility)
}

apiValidation {
    ignoredProjects += ["sample"]
}

allprojects {

    repositories {
        google()
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply from: "$rootDir/cpd.gradle"
    apply from: "$rootDir/detekt.gradle"
    apply from: "$rootDir/ktlint.gradle"
    apply from: "$rootDir/dokka.gradle"
}

gradle.projectsEvaluated {
    subprojects { subproject ->
        def apiCheckTask = subproject.tasks.findByName("apiCheck")
        if (apiCheckTask != null) {
            subproject.tasks.matching { it.name.startsWith("publish") }.all { publishTask ->
                publishTask.dependsOn apiCheckTask
            }
        }
    }
}

apply from: "deploy.gradle"

tasks.withType(JavaCompile) {
    options.compilerArgs += ["--release", "8"]
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task lintAll(dependsOn: [
        ':sentinel:lintRelease',
        ':sentinel-no-op:lintRelease',
        ':tool-chucker:lintRelease',
        ':tool-collar:lintRelease',
        ':tool-dbinspector:lintRelease',
        ':tool-leakcanary:lintRelease',
        ':tool-appgallery:lintRelease',
        ':tool-googleplay:lintRelease'
]) {
    group = "Verification"
    description = "Run Detekt on all modules"
}

task detektAll(dependsOn: [
        ':sentinel:detekt',
        ':sentinel-no-op:detekt',
        ':tool-chucker:detekt',
        ':tool-collar:detekt',
        ':tool-dbinspector:detekt',
        ':tool-leakcanary:detekt',
        ':tool-appgallery:detekt',
        ':tool-googleplay:detekt'
]) {
    group = "Verification"
    description = "Run Detekt on all modules"
}

task ktlintAll(dependsOn: [
        ':sentinel:ktlintCheck',
        ':sentinel-no-op:ktlintCheck',
        ':tool-chucker:ktlintCheck',
        ':tool-collar:ktlintCheck',
        ':tool-dbinspector:ktlintCheck',
        ':tool-leakcanary:ktlintCheck',
        ':tool-appgallery:ktlintCheck',
        ':tool-googleplay:ktlintCheck'
]) {
    group = "Verification"
    description = "Run ktLint on all modules"
}

task cpdAll(dependsOn: [
        ':sentinel:cpdCheck',
        ':sentinel-no-op:cpdCheck',
        ':tool-chucker:cpdCheck',
        ':tool-collar:cpdCheck',
        ':tool-dbinspector:cpdCheck',
        ':tool-leakcanary:cpdCheck',
        ':tool-appgallery:cpdCheck',
        ':tool-googleplay:cpdCheck'
]) {
    group = "Verification"
    description = "Run CPD on all modules"
}

task runStaticChecks(dependsOn: [
        ':lintAll',
        ':detektAll',
        ':ktlintAll',
        ':cpdAll'
]) {
    group = "Verification"
    description = "Run static checks on all modules"
}