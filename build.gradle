buildscript {

    apply from: "config.gradle"
    apply from: "maven.gradle"
    apply from: "dependencies.gradle"

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://dl.bintray.com/infinum/android"
            content {
                includeGroup("com.infinum.thimble")
            }
        }
        gradlePluginPortal()
    }
    dependencies {
        classpath packages.gradle
        classpath packages.kotlin.plugin
        classpath packages.cpd.plugin
        classpath packages.detekt.plugin
        classpath packages.ktlint.plugin
        classpath packages.dokka.plugin
    }
}

allprojects {

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://dl.bintray.com/infinum/android"
            content {
                includeGroup("com.infinum.thimble")
            }
        }
        jcenter() {
            content {
                includeGroup("com.github.skydoves")
                includeModule("com.soywiz.korlibs.korte", "korte-jvm")
                includeModule("org.jetbrains.trove4j", "trove4j")
                includeModule("org.jetbrains.kotlinx", "kotlinx-html-jvm")
                includeGroup("org.jetbrains.dokka")
                includeGroup("org.jetbrains")
                includeGroup("org.koin")
            }
        }
    }

    def buildProperties = new Properties()
    file(rootDir.absolutePath + "/build.properties").withInputStream { buildProperties.load(it) }
    if (buildProperties.getProperty("build.debug").toBoolean()) {
        configurations.all {
            resolutionStrategy {
                dependencySubstitution {
                    substitute module(packages.sentinel.sentinel) with project(':sentinel')
                    substitute module(packages.sentinel.sentinel_no_op) with project(':sentinel-no-op')
                    substitute module(packages.sentinel.tool_chucker) with project(':tool-chucker')
                    substitute module(packages.sentinel.tool_collar) with project(':tool-collar')
                    substitute module(packages.sentinel.tool_dbinspector) with project(':tool-dbinspector')
                    substitute module(packages.sentinel.tool_googleplay) with project(':tool-googleplay')
                    substitute module(packages.sentinel.tool_thimble) with project(':tool-thimble')
                }
            }
        }
    }
}

subprojects {
    apply from: "$rootDir/cpd.gradle"
    apply from: "$rootDir/detekt.gradle"
    apply from: "$rootDir/ktlint.gradle"
    apply from: "$rootDir/dokka.gradle"
}

apply from: "shared.gradle"
apply from: "deploy.gradle"

tasks.withType(JavaCompile) {
    options.compilerArgs += ["--release", "8"]
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detektAll(dependsOn: [
        ':sentinel:detekt',
        ':sentinel-no-op:detekt',
        ':tool-chucker:detekt',
        ':tool-collar:detekt',
        ':tool-dbinspector:detekt',
        ':tool-googleplay:detekt',
        ':tool-thimble:detekt'
]) {
    group = "Verification"
    description = "Run detekt on all modules"
}

task ktlintAll(dependsOn: [
        ':sentinel:ktlintCheck',
        ':sentinel-no-op:ktlintCheck',
        ':tool-chucker:ktlintCheck',
        ':tool-collar:ktlintCheck',
        ':tool-dbinspector:ktlintCheck',
        ':tool-googleplay:ktlintCheck',
        ':tool-thimble:ktlintCheck'
]) {
    group = "Verification"
    description = "Run ktlintCheck on all modules"
}

task cpdAll(dependsOn: [
        ':sentinel:cpdCheck',
        ':sentinel-no-op:cpdCheck',
        ':tool-chucker:cpdCheck',
        ':tool-collar:cpdCheck',
        ':tool-dbinspector:cpdCheck',
        ':tool-googleplay:cpdCheck',
        ':tool-thimble:cpdCheck'
]) {
    group = "Verification"
    description = "Run cpdCheck on all modules"
}

task runStaticChecks(dependsOn: ordered(
        ':detektAll',
        ':ktlintAll',
        ':cpdAll'
)) {
    group = "Verification"
    description = "Run static checks on all modules"
}