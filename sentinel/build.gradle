plugins {
    id "com.android.library"
    id "kotlin-android"
    id "org.jetbrains.kotlin.plugin.serialization"
    id "com.google.devtools.ksp" version "1.6.10-1.0.2"
}

apply from: "jacoco.gradle"

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode releaseConfig.versionCode
        versionName releaseConfig.version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.incremental": "true",
                ]
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
        }
    }

    resourcePrefix "sentinel_"

    lintOptions {
        disable "RtlEnabled", "VectorPath"
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += [
                "-Xopt-in=kotlin.ExperimentalStdlibApi",
                "-Xexplicit-api=strict"
        ]
    }
    testOptions {
        animationsDisabled = true
    }

    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    coreLibraryDesugaring libs.desugar
    implementation libs.kotlin.core
    implementation libs.kotlin.json
    implementation libs.coroutines
    implementation libs.bundles.androidx
    ksp libs.androidx.room.compiler
    implementation libs.material
    implementation libs.koin
    implementation libs.timber

    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "androidx.test:core-ktx:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.security:security-crypto:1.1.0-alpha03"
    androidTestImplementation "org.robolectric:shadowapi:4.5.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.4.0"
    androidTestImplementation "androidx.room:room-testing:2.4.0"
    androidTestImplementation "androidx.fragment:fragment-ktx:1.4.0"
    androidTestImplementation "androidx.fragment:fragment-testing:1.4.0"

    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.test.ext:junit:1.1.3"
}

apply from: "publish.gradle"

project.gradle.taskGraph.whenReady {
    connectedDebugAndroidTest {
        ignoreFailures = true
    }
}