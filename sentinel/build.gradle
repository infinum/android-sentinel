plugins {
    id "com.android.library"
    id "kotlin-android"
    id "org.jetbrains.kotlin.plugin.serialization"
    id "com.google.devtools.ksp" version "2.2.21-2.0.4"
}

apply from: "jacoco.gradle"

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.incremental": "true",
                ]
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
        }
    }

    namespace "com.infinum.sentinel"
    resourcePrefix "sentinel_"

    kotlin {
        jvmToolchain(8)
    }
    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }

    lint {
        disable "RtlEnabled", "VectorPath"
    }
    compileOptions {
    }
    kotlinOptions {
        freeCompilerArgs += [
                "-opt-in=kotlin.ExperimentalStdlibApi",
                "-Xexplicit-api=strict",
                '-Xjvm-default=all',
                "-Xannotation-default-target=param-property"
        ]
    }
    testOptions {
        animationsDisabled = true
    }

    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dokkaJavadoc {
    mustRunAfter(":sentinel:generateReleaseRFile")
    mustRunAfter(":sentinel:kspReleaseKotlin")
}

dependencies {
    implementation libs.kotlin.core
    implementation libs.kotlin.json
    implementation libs.coroutines
    implementation libs.bundles.androidx
    ksp libs.androidx.room.compiler
    implementation libs.material
    ksp libs.inject.compiler
    implementation libs.inject.runtime
    // OkHttp for network emulation
    api "com.squareup.okhttp3:okhttp:4.12.0"

    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.arch.core:core-testing:2.2.0"
    androidTestImplementation "androidx.test:core-ktx:1.5.0"
    androidTestImplementation "androidx.test:rules:1.5.0"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.security:security-crypto:1.1.0-alpha06"
    androidTestImplementation "org.robolectric:shadowapi:4.5.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.5.1"
    androidTestImplementation "androidx.room:room-testing:2.6.1"
    androidTestImplementation "androidx.fragment:fragment-ktx:1.6.2"
    androidTestImplementation "androidx.fragment:fragment-testing:1.6.2"

    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.test.ext:junit:1.1.5"
}

ksp {
    arg("me.tatarka.inject.dumpGraph", "false")
}

apply from: "publish.gradle"

project.gradle.taskGraph.whenReady {
    connectedDebugAndroidTest {
        ignoreFailures = true
    }
}

apply from: '../tasks.gradle'
preBuild.dependsOn ':sentinel:generateReadme'

apply from:'../apiComparison.gradle'
afterEvaluate {
    tasks.named("apiCheck").configure {
        dependsOn("compareSentinelApis")
    }
}

