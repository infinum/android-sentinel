plugins {
    id "com.android.library"
    id "kotlin-android"
    id "org.jetbrains.kotlin.plugin.serialization"
    alias(libs.plugins.ksp)
}

apply from: "jacoco.gradle"

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.incremental": "true",
                ]
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
        }
    }

    namespace "com.infinum.sentinel"
    resourcePrefix "sentinel_"

    kotlin {
        jvmToolchain(8)
    }
    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }

    lint {
        disable "RtlEnabled", "VectorPath"
    }
    compileOptions {
    }
    kotlinOptions {
        freeCompilerArgs += [
                "-opt-in=kotlin.ExperimentalStdlibApi",
                "-Xexplicit-api=strict",
                '-Xjvm-default=all',
                "-Xannotation-default-target=param-property"
        ]
    }
    testOptions {
        animationsDisabled = true
    }

    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dokkaJavadoc {
    mustRunAfter(":sentinel:generateReleaseRFile")
    mustRunAfter(":sentinel:kspReleaseKotlin")
}

dependencies {
    implementation libs.kotlin.core
    implementation libs.kotlin.json
    implementation libs.coroutines
    implementation libs.bundles.androidx
    ksp libs.androidx.room.compiler
    implementation libs.material
    ksp libs.inject.compiler
    implementation libs.inject.runtime

    androidTestImplementation libs.junit
    androidTestImplementation libs.androidx.arch.core.testing
    androidTestImplementation libs.androidx.test.core.ktx
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.crypto
    androidTestImplementation libs.robolectric.shadowapi
    androidTestImplementation libs.androidx.test.espresso.core
    androidTestImplementation libs.androidx.test.espresso.intents
    androidTestImplementation libs.androidx.room.testing
    androidTestImplementation libs.androidx.fragment
    androidTestImplementation libs.androidx.fragment.testing

    testImplementation libs.junit
    testImplementation libs.androidx.test.ext.junit
}

ksp {
    arg("me.tatarka.inject.dumpGraph", "false")
}

apply from: "publish.gradle"

project.gradle.taskGraph.whenReady {
    connectedDebugAndroidTest {
        ignoreFailures = true
    }
}

apply from: '../tasks.gradle'
preBuild.dependsOn ':sentinel:generateReadme'

apply from:'../apiComparison.gradle'
afterEvaluate {
    tasks.named("apiCheck").configure {
        dependsOn("compareSentinelApis")
    }
}

