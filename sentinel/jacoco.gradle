// Jacoco test coverage configuration (Groovy)
// Configures code coverage reporting for unit and instrumented tests
apply plugin: 'jacoco'

android {
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
    }
}

jacoco {
    toolVersion = libs.versions.jacoco.get()
    reportsDirectory = file("${buildDir}/reports/jacoco")
}

tasks.register('jacocoTestReport', JacocoReport) {
    dependsOn 'testDebugUnitTest', 'createDebugCoverageReport'
    group = 'Reporting'
    description = 'Generate Jacoco coverage reports for both unit and instrumented unit tests'

    reports {
        csv.required = false
        xml.required = true
        html.required = true

        xml.outputLocation = file("${jacoco.reportsDirectory.get()}/jacocoTestReport.xml")
        html.outputLocation = file("${jacoco.reportsDirectory.get()}/html")
    }

    def excludesFilter = [
        '**/*Test*.*',
        '**/AutoValue_*.*',
        '**/*JavascriptBridge.class',
        '**/R.class',
        '**/R$*.class',
        '**/Manifest*.*',
        'android/**/*.*',
        '**/BuildConfig.*',
        '**/*$ViewBinder*.*',
        '**/*$ViewInjector*.*',
        '**/Lambda$*.class',
        '**/Lambda.class',
        '**/*Lambda.class',
        '**/*Lambda*.class',
        '**/lambda$*.class',
        '**/lambda.class',
        '**/*lambda.class',
        '**/*lambda*.class',
        '**/*$$special$$inlined$inject$*.*',
        '**/*$InjectAdapter.class',
        '**/*$ModuleAdapter.class',
        '**/*$ViewInjector*.class',
        '**/*_MembersInjector.class',
        '*/*_MembersInjector*.*',
        '**/*_*Factory*.*',
        '*/*Component*.*',
        '**/*Module*.*'
    ]

    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: excludesFilter) +
                    fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: excludesFilter)

    sourceDirectories.setFrom(files("${project.projectDir}/src/main/kotlin"))
    classDirectories.setFrom(files(debugTree))
    executionData.setFrom(fileTree(buildDir) {
        include(
            'jacoco/testDebugUnitTest.exec',
            'outputs/code_coverage/debugAndroidTest/connected/*coverage.ec'
        )
    })
}
