apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"
apply plugin: 'com.github.dcendents.android-maven'

group = sentinel.group
version = sentinel.version

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

task javadocsJar(type: Jar, dependsOn: rootProject.dokka) {
    archiveClassifier.set('javadoc')
    from rootProject.dokka.outputDirectory
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

publishing {
    publications {
        infinum(MavenPublication) {
            groupId = group
            artifactId = 'sentinel'

            pom {
                name = 'Sentinel'
                description = 'Single point of entry for all provided tools, app and device information.'
                url = 'https://github.com/infinum/android-sentinel'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
            pom.withXml {
                def root = asNode()
                def dependenciesNode = root.appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    def bintrayProperties = rootProject.file('bintray.properties')
    if (bintrayProperties.exists()) {
        properties.load(new FileInputStream(bintrayProperties))
    } else {
        properties.setProperty("bintray.user", "")
        properties.setProperty("bintray.apikey", "")
        properties.setProperty("bintray.gpg_passphrase", "")
    }

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    pkg {
        repo = 'android'
        name = 'sentinel'
        userOrg = 'infinum'
        desc = 'Sentinel package'
        websiteUrl = 'https://github.com/infinum/android-sentinel'
        issueTrackerUrl = 'https://github.com/infinum/android-sentinel/issues'
        vcsUrl = 'https://github.com/infinum/android-sentinel.git'
        licenses = ['Apache-2.0']
        version {
            name = "${sentinel.version}"
            released = new Date()
            vcsTag = "${sentinel.version}"
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg_passphrase")
            }
        }
        publish = true
        override = true
        publicDownloadNumbers = true
    }

    configurations = ['archives']
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'
                groupId 'com.infinum.sentinel'
                artifactId 'sentinel'

                name 'sentinel'
                description 'Sentinel'
                url 'https://github.com/infinum/android-sentinel'

                licenses {
                    license {
                        name 'Apache'
                        url 'https://github.com/infinum/android-sentinel/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'knobtviker'
                        name 'Bojan KomljenoviÄ‡'
                        email 'bojan.komljenovic@infinum.com'
                    }
                }
                scm {
                    connection 'https://github.com/infinum/android-sentinel.git'
                    developerConnection 'https://github.com/infinum/android-sentinel.git'
                    url 'https://github.com/infinum/android-sentinel'
                }
            }
        }
    }
}