plugins {
    id "com.android.library"
    id "kotlin-android"
}

android {
    buildFeatures {
        buildConfig = false
    }

    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
        }
    }

    namespace "com.infinum.sentinel"

    kotlin {
        jvmToolchain(8)
    }
    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }

    kotlinOptions {
        freeCompilerArgs += [
                '-Xexplicit-api=strict',
                '-Xjvm-default=all'
        ]
    }

    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dokkaJavadoc {
    mustRunAfter(":tool-leakcanary:generateReleaseRFile")
}

dependencies {
    coreLibraryDesugaring libs.desugar
    implementation libs.kotlin.core
    api libs.library
    api libs.leakcanary
}

apply from: "publish.gradle"
